Execute (vscrib#Substitute: Setup):
  let g:vscrib = vscrib#New()
  execute 'cd '.g:test_directories.'/good_parent/empty_child'
  edit blah.md
  call g:vscrib.Refresh()

Execute (vscrib#Substitute: No Substitutions):
  AssertEqual '"foobar": "Bar Foo Bar"',
      \ g:vscrib.Substitute('"foobar": "Bar Foo Bar"', 0, 1)

Execute (vscrib#Substitute: VSCode Predefined Variables):
  let g:workspace_basename = 'good_parent'
  let g:workspace = g:test_directories.'/'.g:workspace_basename
  let g:cwd = g:test_directories.'/good_parent/empty_child'
  let g:filename_no_extension = 'blah'
  let g:filename_extension = '.md'
  let g:filename_basename = g:filename_no_extension.g:filename_extension
  let g:file_dirname = g:test_directories.'/good_parent/empty_child'
  let g:filename = g:file_dirname.'/'.g:filename_basename
  let g:curpos = [0, 1, 1, 0, 1]  " not sure why, but this is what it is?
  let g:selection = ''  " NOTE: testing this with Vader is impractical
  let g:vscode = '/NO_VSCODE_EXE_SPECIFIED'
  let g:expected = {
      \ 'workspaceFolder': g:workspace,
      \ 'workspaceFolderBasename': g:workspace_basename,
      \ 'file': g:filename,
      \ 'relativeFile': 'empty_child/blah.md',
      \ 'fileBasename': g:filename_basename,
      \ 'fileBasenameNoExtension': g:filename_no_extension,
      \ 'fileDirname': g:file_dirname,
      \ 'fileExtname': g:filename_extension,
      \ 'cwd': g:cwd,
      \ 'lineNumber': g:curpos[1],
      \ 'selectedText': g:selection,
      \ 'execPath': g:vscode,
      \ }
  for [g:var, g:exp] in items(g:expected)
    AssertEqual '"foobar": "'.g:exp.'"',
        \ g:vscrib.Substitute('"foobar": "${'.g:var.'}"', 0, 1)
  endfor

Execute (vscrib#Substitute: Multiple Substitutions Single Line):
  AssertEqual '"foobar:" ".md /NO_VSCODE_EXE_SPECIFIED '.g:workspace.'"',
      \ g:vscrib.Substitute('"foobar:" "${fileExtname} ${execPath} ${workspaceFolder}"')

Execute (vscrib#Substitute: Unrecognized Variable):
  AssertThrows call g:vscrib.Substitute('"foobar": "${foobar}"', 0, 1)
  AssertEqual 0, match(g:vader_exception, '^ERROR(NotImplemented)'),
      \ 'Threw bad exception: '.g:vader_exception

Execute (vscrib#Substitute: Silently Ignore Unrecognized Variable):
  AssertEqual '"foobar": "${foobar}"',
      \ g:vscrib.Substitute('"foobar": "${foobar}"', 1, 1)

Execute (vscrib#Substitute: Prompt for User Input (Non-Interactive)):
  AssertThrows call g:vscrib.Substitute(
      \ '"foobar": "${prompt:Prompt message}"', 0, 1)
  AssertEqual 0, match(g:vader_exception, '^ERROR(BadValue)'),
      \ 'Threw bad exception: '.g:vader_exception

Do (vscrib#Substitute: Prompt for User Input (Interactive)):
  :unlet g:actual\<CR>
  :let g:actual = g:vscrib.Substitute('"foobar": "${prompt:Prompt message}"', 0, 0, 1)\<CR>
  silver on the tree\<CR>
Then:
  AssertEqual '"foobar": "silver on the tree"', g:actual

Execute (vscrib#Substitute: Clean Up):
  execute 'cd '.g:test_dir
